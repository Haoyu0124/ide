/*******************************************************************************
 * Copyright (c) 2019 Atos Spain S.A.
 * This program and the accompanying materials
 * are made available under the terms of the Apache Public License 2.0 (APL2)
 * which accompanies this distribution, and is available at
 * https://apache.org/licenses/LICENSE-2.0
 *
 * Contributors:
 *     Jesús Gorroñogoitia - Design and implementation
 *******************************************************************************/
//grammar org.sodalite.dsl.AADM with org.eclipse.xtext.common.Terminals
grammar org.sodalite.dsl.AADM with org.sodalite.dsl.RM

generate aADM "http://www.sodalite.org/dsl/AADM"
import "http://www.sodalite.org/dsl/RM" as rm
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AADM_Model:
	('inputs{'
		inputs = EInputs
	'}')? &
	('node_templates{'
		nodeTemplates=ENodeTemplates
	'}')?;


ENodeTemplates:
	{ENodeTemplates} (nodeTemplates+=ENodeTemplate)+;

ENodeTemplate:
	name=ID '{'
	node=ENodeTemplateBody
	"}";

ENodeTemplateBody:
	('type:' type=QUALIFIED_NAME) &
	('description:' description=STRING)? &
	('properties{'
	properties=EPropertyAssigments
	'}')? &
	('attributes{'
	atributes=EAttributeAssigments
	'}')? &
	('requirements{'
	requirements=ERequirementAssignments
	'}')? &
	('capabilities{'
	capabilities=ECapabilityAssignments
	'}')?
	;

ERequirementAssignments:
	{ERequirementAssignments} (requirements+=ERequirementAssignment)*;

ERequirementAssignment:
	name=ID '{'
	('node:' node=[ENodeTemplate|ID])
	'}';

ECapabilityAssignments:
	{ECapabilityAssignments} (capabilities+=ECapabilityAssignment)*;

ECapabilityAssignment:
	name=ID '{'
	('properties{'
	properties=EPropertyAssigments
	'}')? 
	'}';

EAttributeAssigments:
	{EAttributeAssigments} (attributes+=EAttributeAssignment)*;

EAttributeAssignment:
	name=ID ':' value=STRING;

EPropertyAssigments:
	{EPropertyAssigments} (properties+=EPropertyAssignment)*;

EPropertyAssignment:
	name=ID ':' value=EPropertyAssignmentValue;


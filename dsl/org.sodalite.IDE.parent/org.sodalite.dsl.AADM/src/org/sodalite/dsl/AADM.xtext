/*******************************************************************************
 * Copyright (c) 2019 Atos Spain S.A.
 * This program and the accompanying materials
 * are made available under the terms of the Apache Public License 2.0 (APL2)
 * which accompanies this distribution, and is available at
 * https://apache.org/licenses/LICENSE-2.0
 *
 * Contributors:
 *     Jesús Gorroñogoitia - Design and implementation
 *******************************************************************************/
//grammar org.sodalite.dsl.AADM with org.eclipse.xtext.common.Terminals
grammar org.sodalite.dsl.AADM with org.sodalite.dsl.RM

generate aADM "http://www.sodalite.org/dsl/AADM"
import "http://www.sodalite.org/dsl/RM" as rm
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AADM_Model:
	('inputs'START_BLOCK
		inputs = EInputs
	END_BLOCK)? &
	('node_templates'START_BLOCK
		nodeTemplates=ENodeTemplates
	END_BLOCK)?;


ENodeTemplates:
	{ENodeTemplates} (nodeTemplates+=ENodeTemplate)+;

ENodeTemplate:
	name=ID START_BLOCK
	node=ENodeTemplateBody
	END_BLOCK;

ENodeTemplateBody:
	('type:' type=QUALIFIED_NAME) &
	('description:' description=STRING)? &
	('properties'START_BLOCK
	properties=EPropertyAssigments
	END_BLOCK)? &
	('attributes'START_BLOCK
	atributes=EAttributeAssigments
	END_BLOCK)? &
	('requirements'START_BLOCK
	requirements=ERequirementAssignments
	END_BLOCK)? &
	('capabilities'START_BLOCK
	capabilities=ECapabilityAssignments
	END_BLOCK)?
	;

ERequirementAssignments:
	{ERequirementAssignments} (requirements+=ERequirementAssignment)*;

ERequirementAssignment:
	name=ID START_BLOCK
	('node:' node=[ENodeTemplate|ID])
	END_BLOCK;

ECapabilityAssignments:
	{ECapabilityAssignments} (capabilities+=ECapabilityAssignment)*;

ECapabilityAssignment:
	name=ID START_BLOCK
	('properties{'
	properties=EPropertyAssigments
	END_BLOCK)? 
	END_BLOCK;

EAttributeAssigments:
	{EAttributeAssigments} (attributes+=EAttributeAssignment)*;

EAttributeAssignment:
	name=ID ':' value=STRING;

EPropertyAssigments:
	{EPropertyAssigments} (properties+=EPropertyAssignment)*;

EPropertyAssignment:
	name=ID ':' value=EPropertyAssignmentValue;
	


